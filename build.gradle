buildscript {
  repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
  }

  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:2.2.0.RELEASE"
    classpath "io.spring.gradle:dependency-management-plugin:1.0.8.RELEASE"
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+"
  }
}

apply plugin: "idea"
apply plugin: "eclipse"

group "kr.lul.support.spring"
version "0.0.1.SNAPSHOT"
description """
"""

allprojects {
  ext {
    COMMON_VERSION = "0.0.1.SNAPSHOT"
  }

  repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
    maven {
      url "https://dl.bintray.com/justburrow/snapshot"
    }
    maven {
      url "https://dl.bintray.com/justburrow/release"
    }
  }
}

subprojects {
  apply plugin: "java"
  apply plugin: "org.springframework.boot"
  apply plugin: "io.spring.dependency-management"

  apply plugin: "maven-publish"
  apply plugin: "com.jfrog.bintray"

  sourceCompatibility = 11
  targetCompatibility = 11

  dependencyManagement {
    imports {
      mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }

    dependencies {
      dependency "kr.lul.common:common-util:$COMMON_VERSION"

      dependency "org.assertj:assertj-core:3.14.0"
      dependency "org.apache.commons:commons-lang3:3.9"
    }
  }

  dependencies {
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.assertj:assertj-core"
  }

  def projectGroup = rootProject.group
  def projectName = project.name
  def projectVersion = "unspecified" != project.version
      ? project.version
      : rootProject.version
  def isRelease = projectVersion.endsWith("RELEASE")
  def isSnapshot = projectVersion.endsWith("SNAPSHOT")

  println("""
      subproject ========================================
      group : $projectGroup
      name : $projectName
      version : $projectVersion
      release : $isRelease
      snapshot : $isSnapshot
      ===================================================
      """.stripIndent())

  jar.enabled = true
  bootJar.enabled = false

  task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
  }

  task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
  }

  artifacts {
    archives sourcesJar, javadocJar
  }

  publishing {
    publications {
      mavenPublication(MavenPublication) {
        from components.java

        artifact sourcesJar
        artifact javadocJar

        groupId projectGroup
        artifactId projectName
        version projectVersion
      }
    }
  }

  bintray {
    user = BINTRAY_USER
    key = BINTRAY_API_KEY

    pkg {
      repo = isRelease ? "release" : "snapshot"
      name = projectName
      desc = project.description

      websiteUrl = "https://blog.lul.kr"
      issueTrackerUrl = "https://github.com/JustBurrow/support-spring/issues"
      vcsUrl = "https://github.com/JustBurrow/support-spring.git"
      licenses = ["Apache-2.0"]

      publicDownloadNumbers = true
      publish = true
      override = isSnapshot
      publications = ['mavenPublication']

      githubRepo = "JustBurrow/support-spring"
      githubReleaseNotesFile = "README.md"

      version {
        name = projectVersion
        desc = project.description
      }
    }
  }
}
