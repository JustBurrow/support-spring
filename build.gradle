buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
  }

  dependencies {
    classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.4.4'
    classpath 'io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE'
  }
}

apply plugin: 'idea'
apply plugin: 'eclipse'

allprojects {
  ext {
    COMMON_VERSION = '0.0.1.SNAPSHOT'
  }

  group 'kr.lul.support.spring'
  version '0.0.1.SNAPSHOT'

  repositories {
    mavenLocal()
    mavenCentral()
  }
}

subprojects {
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'java'
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'
  apply plugin: 'org.springframework.boot'

  sourceCompatibility = 11

  dependencyManagement {
    imports {
      mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }

    dependencies {
      dependency "kr.lul.common:common-data:$COMMON_VERSION"
      dependency "kr.lul.common:common-logging:$COMMON_VERSION"
      dependency "kr.lul.common:common-util:$COMMON_VERSION"
      dependency "kr.lul.common:common-web:$COMMON_VERSION"
      dependency 'org.apache.commons:commons-lang3:3.12.0'
      dependency 'com.github.bufferings:thymeleaf-extras-nl2br:1.0.2'
    }
  }

  dependencies {
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
  }

  bootJar.enabled = false
  jar.enabled = true

  java {
    withJavadocJar()
    withSourcesJar()
  }

  javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
      options.addBooleanOption('html5', true)
    }
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
        versionMapping {
          usage('java-api') {
            fromResolutionOf('runtimeClasspath')
          }
          usage('java-runtime') {
            fromResolutionResult()
          }
        }
        pom {
          url = 'https://github.com/JustBurrow/support-spring'
          licenses {
            license {
              name = 'The Apache License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }
          developers {
            developer {
              id = 'JustBurrow'
              name = 'Just Burrow'
              email = 'just.burrow@lul.kr'
            }
          }
        }
      }
    }
  }

  publishToMavenLocal {
    dependsOn clean
    dependsOn test

    test.mustRunAfter clean
  }

  test {
    enabled = true
    useJUnitPlatform()
  }
}
