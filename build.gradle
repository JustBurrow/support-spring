buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
  }

  dependencies {
    classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.4.4'
    classpath 'io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE'
  }
}

apply plugin: 'idea'
apply plugin: 'eclipse'

allprojects {
  group 'kr.lul.support.spring'
  version '0.0.1.SNAPSHOT'

  repositories {
    mavenLocal()
    mavenCentral()
  }
}

subprojects {
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'java'
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'
  apply plugin: 'org.springframework.boot'

  sourceCompatibility = JavaVersion.VERSION_11

  dependencyManagement {
    imports {
      //noinspection UnnecessaryQualifiedReference
      mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }

    def commonVersion = '0.0.1.SNAPSHOT'
    dependencies {
      dependency "kr.lul.common:common-data:$commonVersion"
      dependency "kr.lul.common:common-logging:$commonVersion"
      dependency "kr.lul.common:common-util:$commonVersion"
      dependency "kr.lul.common:common-web:$commonVersion"

      dependency 'com.github.bufferings:thymeleaf-extras-nl2br:1.0.2'
      dependency 'io.github.jpenren:thymeleaf-spring-data-dialect:3.4.0'
      dependency 'org.apache.commons:commons-lang3:3.12.0'
      dependency 'org.apache.commons:commons-text:1.9'
    }
  }

  dependencies {
    testImplementation 'org.apache.commons:commons-lang3'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
  }

  bootJar.enabled = false
  jar.enabled = true

  java {
    withJavadocJar()
    withSourcesJar()
  }

  javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
      options.addBooleanOption('html5', true)
    }
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
        versionMapping {
          usage('java-api') {
            fromResolutionOf('runtimeClasspath')
          }
          usage('java-runtime') {
            fromResolutionResult()
          }
        }
        pom {
          url = 'https://github.com/JustBurrow/support-spring'
          licenses {
            license {
              name = 'The Apache License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }
          developers {
            developer {
              id = 'JustBurrow'
              name = 'Just Burrow'
              email = 'just.burrow@lul.kr'
            }
          }
        }
      }
    }
  }

  publishToMavenLocal {
    dependsOn clean
    dependsOn test

    test.mustRunAfter clean
  }

  test {
    enabled = true
    useJUnitPlatform()
  }
}
